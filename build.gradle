/*
* Copyright Â© 2017-2021  Kynetics  LLC
*
* This program and the accompanying materials are made
* available under the terms of the Eclipse Public License 2.0
* which is available at https://www.eclipse.org/legal/epl-2.0/
*
* SPDX-License-Identifier: EPL-2.0
*/

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
    id 'io.gitlab.arturbosch.detekt' version '1.17.1'
    id 'jacoco'
    id 'java'
    id("org.jetbrains.dokka") version "1.5.30"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

repositories {
    mavenCentral()
}

final def deps = gradle.settings.app_dependencies
final def app_version = '1.0.0'

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply from: "$rootDir/ktlint.gradle"
    apply plugin: 'java'
    apply plugin: "org.jetbrains.dokka"

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

project(':ddiapi-client') {
    group 'org.eclipse.hara.hara-ddiclient'
    version app_version

    final def subprj = ':ddiapi-client:ddiapi-client-api'
    dependencies {
        implementation project(subprj)
        implementation deps.retrofit_converter
        implementation deps.kotlin_stdlib
        implementation deps.kotlin_x
    }
    allprojects {
        dependencies {
            implementation deps.okhttp
            implementation deps.retrofit
        }
    }
    project(subprj) {
        dependencies {
            implementation deps.gson
        }
    }
}

project(':ddiapi-client:ddiapi-client-api') {
    group 'org.eclipse.hara.hara-ddiclient.ddiapi-client'
    version app_version

    dependencies {
        implementation deps.joda
        implementation deps.kotlin_stdlib
        implementation deps.kotlin_x
    }
}
